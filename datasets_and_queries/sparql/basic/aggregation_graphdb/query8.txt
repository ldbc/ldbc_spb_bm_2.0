PREFIX bbcevent:<http://www.bbc.co.uk/ontologies/event/> 
PREFIX geo-pos:<http://www.w3.org/2003/01/geo/wgs84_pos#> 
PREFIX bbc:<http://www.bbc.co.uk/ontologies/bbc/> 
PREFIX time:<http://www.w3.org/2006/time#> 
PREFIX event:<http://purl.org/NET/c4dm/event.owl#> 
PREFIX music-ont:<http://purl.org/ontology/mo/> 
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX foaf:<http://xmlns.com/foaf/0.1/> 
PREFIX provenance:<http://www.bbc.co.uk/ontologies/provenance/> 
PREFIX owl:<http://www.w3.org/2002/07/owl#> 
PREFIX cms:<http://www.bbc.co.uk/ontologies/cms/> 
PREFIX news:<http://www.bbc.co.uk/ontologies/news/> 
PREFIX cnews:<http://www.bbc.co.uk/ontologies/news/cnews/> 
PREFIX cconcepts:<http://www.bbc.co.uk/ontologies/coreconcepts/> 
PREFIX dbp-prop:<http://dbpedia.org/property/> 
PREFIX geonames:<http://sws.geonames.org/> 
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#> 
PREFIX domain:<http://www.bbc.co.uk/ontologies/domain/> 
PREFIX dbpedia:<http://dbpedia.org/resource/> 
PREFIX geo-ont:<http://www.geonames.org/ontology#> 
PREFIX bbc-pont:<http://purl.org/ontology/po/> 
PREFIX tagging:<http://www.bbc.co.uk/ontologies/tagging/> 
PREFIX sport:<http://www.bbc.co.uk/ontologies/sport/> 
PREFIX skosCore:<http://www.w3.org/2004/02/skos/core#> 
PREFIX dbp-ont:<http://dbpedia.org/ontology/> 
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#> 
PREFIX core:<http://www.bbc.co.uk/ontologies/coreconcepts/> 
PREFIX curric:<http://www.bbc.co.uk/ontologies/curriculum/> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#> 
PREFIX cwork:<http://www.bbc.co.uk/ontologies/creativework/> 
PREFIX fb:<http://rdf.freebase.com/ns/>
PREFIX geo:<http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX ldbc:<http://www.ldbc.eu/ontologies/sempub#>
PREFIX luceneConnector:<http://www.ontotext.com/connectors/lucene#>
PREFIX inst:<http://www.ontotext.com/connectors/lucene/instance#>
PREFIX ot:<http://www.ontotext.com/>

#  Query name : query8
#  Query Description : 
#  Retrieve creative works and their properties, which contain a a certain word in their title or description. 
#  A Full-text search query. Requires enabled GraphDB Lucene Connector.

#  To enable GraphDB Lucene Connector :
#  GraphDB-EE version is required, execute query: 
#
#  PREFIX luceneConnector: <http://www.ontotext.com/connectors/lucene#>
#  PREFIX inst: <http://www.ontotext.com/connectors/lucene/instance#>
#  INSERT DATA {
#    inst:cwLuceneConnectorIndex luceneConnector:createConnector '''
#{
#  "types": [
#    "http://www.bbc.co.uk/ontologies/creativework/CreativeWork"
#  ],
#  "fields": [
#    {
#      "fieldName": "title",
#      "propertyChain": [
#        "http://www.bbc.co.uk/ontologies/creativework/title"
#      ],
#      "facet": false
#    },
#    {
#      "fieldName": "description",
#      "propertyChain": [
#        "http://www.bbc.co.uk/ontologies/creativework/description"
#      ],
#      "facet": false
#    },
#    {
#      "fieldName": "dateModified",
#      "propertyChain": [
#        "http://www.bbc.co.uk/ontologies/creativework/dateModified"
#      ],
#      "sort": true
#      "noindex": true,
#      "facet": false
#    }
#  ]
#}
#''' .
#}
#
# Management of GraphDB Lucene Connector
# DROP CONNECTOR
# ---------------------
# PREFIX luceneConnector:<http://www.ontotext.com/connectors/lucene#>
# PREFIX inst: <http://www.ontotext.com/connectors/lucene/instance#>
# INSERT DATA {
#  inst:cwLuceneConnectorIndex luceneConnector:dropConnector "" .
# }
#
# LIST CONNECTORS
# ----------------------
# PREFIX luceneConnector:<http://www.ontotext.com/connectors/lucene#>
# SELECT ?cntUri ?cntStr {
#  ?cntUri luceneConnector:listConnectors ?cntStr .
# }
#
# CONNECTOR STATUS CHECK
# ----------------------
# PREFIX luceneConnector:<http://www.ontotext.com/connectors/lucene#> 
# SELECT ?cntUri ?cntStatus {
#   ?cntUri luceneConnector:connectorStatus ?cntStatus .
# }

#  Choke Points : 
#    - OPTIONAL clauses (treated by query optimizer as nested sub-queries)
#      Optimizer should decide to put optional triples on top of the join tree 
#      (i.e. delay their execution to the last possible moment) because OPTIONALs are treated as a left join
#    - A possibility for optimizing the full-text search by using appropriate index

CONSTRUCT { 
  ?creativeWork a cwork:CreativeWork ; 
    a ?type ; 
    cwork:title ?title ; 
    cwork:description ?description ; 
    cwork:dateCreated ?dateCreated ;
    cwork:dateModified ?dateModified ;
    cwork:category ?category ; 
    bbc:primaryContentOf ?pco . 
  ?pco bbc:webDocumentType ?webDocType . 
} 
FROM ot:disable-sameAs
WHERE { 
  {
  SELECT * {
        ?search a inst:cwLuceneConnectorIndex ;
            luceneConnector:query "title:{{{word}}} description:{{{word}}}" ;
            luceneConnector:orderBy "-dateModified" ;
            luceneConnector:entities ?creativeWork .
        ?creativeWork cwork:title ?title ;
               cwork:description ?description ;            
               cwork:dateModified ?dateModified .
    }
  }
  ?creativeWork a ?type .
  ?type rdfs:subClassOf cwork:CreativeWork . 
  ?creativeWork cwork:dateModified ?dateModified . 
  OPTIONAL { ?creativeWork cwork:dateCreated ?dateCreated . }
  OPTIONAL { ?creativeWork cwork:category ?category . }  
  OPTIONAL { 
    ?creativeWork bbc:primaryContentOf ?pco . 
    ?pco bbc:webDocumentType ?webDocType . 
  } 
}
LIMIT {{{randomLimit}}}